#AUXILIARY FUNCTIONS

PolynomialToFx := proc(f,p,pol)
# Returns a polynomial with coefficients in F.
# If |F| = q = p, write pol = 1 and the polynomial will be converted in a polynomial with coefficients in Zp.
local normalized, result, j, coefficient; 
if pol = 1 then
    result := 0;
    for j from 0 to degree(f,x) do
        coefficient := coeff(f,x,j) mod p;
        result :=result + coefficient*x^(j);
    od;
    normalized := normal(result);
else
    result := 0;
    for j from 0 to degree(f, x) do
        coefficient := rem(coeff(f, x, j), pol, alpha) mod p;
        result := result + coefficient*x^j;
    od;
    normalized := normal(result);
fi:
return(normalized)
end:

GCDFinite:=proc(f,g,p,pol)
    # Returns the G.C.D. of two primitive polynomials with coefficients in F.
local r, n, i, coef, normalized;
r[0] := f;
r[1] := g;

n[0] := degree(f, x);
n[1] := degree(g, x);

i:=1;  
while r[i]<>0 do
    coef := PolynomialToFx(lcoeff(r[i], x)^(n[i - 1] - n[i] + 1), p, pol);
    r[i + 1] := primpart(rem(PolynomialToFx(coef*r[i - 1], p, pol), r[i], x), x);
    r[i + 1] := primpart(PolynomialToFx(r[i + 1], p, pol), x);
    n[i + 1] := degree(r[i + 1], x);
    i := i + 1;
od:
normalized := normal(r[i - 1]);
return(normalized)  
end: 

# 1. SFD (Square-free descomposition)

SFD:=proc(f,p,w,pol)
_local(F, L, s, j, k, g, h, m, differentialF, G, H);
F := f;
L := [];
s := 1;
while (F<>1) do
    j := 1;
    if PolynomialToFx(diff(F,x),p,pol)=0 then
        differentialF :=1;
    else
        differentialF := PolynomialToFx(diff(F, x), p, pol);
    fi:
    g:=PolynomialToFx(quo(F,GCDFinite(F,differentialF,p,pol),x),p,pol);      
    while g<>1 do     
    F := PolynomialToFx(quo(F, g, x), p, pol);
    h := GCDFinite(F, g, p, pol);
    m := PolynomialToFx(quo(g, h, x), p, pol);
    if m<>1 then
        L:=[op(L),[m,j*s]];
    fi:
    g := h;
    j := j + 1;
    od:
    if F<>1 then #then F is an G(X^(p)), which can be put as  H^(p) using a formula.
        G := PolynomialToFx(quo(F, x^p, x) + rem(F, x^p, x), p, pol);
        H := 0;
        for k  from 0 to degree(G,x) do    
            H:=H + coeff(G,x,k)^(p^((w-1)))*x^(k);
        od;
        F := PolynomialToFx(H, p, pol);
        s := p*s;
    fi:
od:
return(L)
end:

#EXAMPLES SFD:

p := 2:
w := 2:
pol := alpha^2 + alpha + 1:
f := PolynomialToFx((x^2 + alpha)^2*(x + 1), p, pol):
SFD(f, p, w, pol) #Result: [[x + 1, 1], [x^2 + alpha, 2]]

p := 3:
w := 3:
pol := alpha^3 + alpha^2 + alpha - 1:
f := PolynomialToFx((x^2 + alpha)^2*(x - 2), p, pol):
SFD(f, p, w, pol); #Result: [[x + 1, 1], [x^2 + alpha, 2]]

p := 5:
w := 1:
pol := 1:
f := PolynomialToFx((x^2 + 1)^3*x, p, pol):
SFD(f, p, w, pol); #Result: [[x, 1], [x^2 + 1, 3]]

p := 7:
w := 1:
pol := 1:
SFD(x^3 + 2*x^2 + x, p, w, pol); #Result: [[x, 1], [x + 1, 2]]


# 2. DDF (Distinct Degree Factorization)

DDF:=proc(f,p,w,pol)
_local(L, F, X, h, k, g, q);
q := p^w;
L := [];
F := f;
h := rem(x, F, x);
k := 0;
while F<>1 do
    h := PolynomialToFx(rem(h^q, F, x), p, pol);
    k := k + 1;
    g := GCDFinite(h - x, F, p, pol);
    if g<>1 then
        F := PolynomialToFx(quo(F, g, x), p, pol);
        h := PolynomialToFx(rem(h, F, x), p, pol);
        L := [op(L), [g, k]];
    fi:
od:
return(L)
end:

#EXAMPLES DDF:

p := 5:
w := 1:
pol := 1:
f := PolynomialToFx((x^2 + x + 1)*(x - 5), p, pol):
DDF(f, p, w, pol); #Result: [[x, 1], [x^2 + x + 1, 2]]

p := 2:
w := 2:
pol := alpha^2 + alpha + 1:
f := PolynomialToFx((x^2 + alpha + x)*(x^3 + 1), p, pol):
DDF(f, p, w, pol); #Result: [[x^3 + 1, 1], [x^2 + alpha + x, 2]]

p := 3:
w := 3:
pol := alpha^3 + alpha^2 + alpha - 1:
f := PolynomialToFx(x^4 + 2*x^3 + alpha*x + alpha + 2*x, p, pol):
DDF(f, p, w, pol); #Result: [[x + 1, 1], [x^3 + x^2 + alpha + 2*x, 3]]


# 3. EDF (Equal Degree Factorization)

Mk:=proc(polynomial,w,k,p,pol)
local M, representation;
M := sum(polynomial^(p^j), j = 0 .. (k*w - 1));
representation := PolynomialToFx(M,p,pol);
return(representation)
end:

EDF:=proc(f,p,w, pol,k)
local l, r, H, A, h, a, d, t;
l := degree(f);
r := l/k;
H := {f};
while nops(H)<r do
    A:={};
    for h in H do
        a := randpoly(x);
        a := rem(a, h, x);
        a := PolynomialToFx(a, p, pol);
        d := PolynomialToFx(GCDFinite(Mk(a, w, k, p, pol), h, p, pol), p, pol);
        if (d=1 or d=h) then
            A := A union {h};
        else
            t := PolynomialToFx(quo(h, d, x), p, pol);
            A := A union {d, t};
        fi:
    od:
    H:=A;
od:
return(H)
end:

#EXAMPLES EDF:

p := 5:
w := 1:
pol := 1:
f := PolynomialToFx(x^3 + x^2 + x, p, pol):
EDF(f, p, w, pol, 2); #Result: {x, x^2 + x + 1}

p := 2:
w := 2:
pol := alpha^2 + alpha + 1:
f := PolynomialToFx((x + 1)*(x + alpha), p, pol):
EDF(f, p, w, pol, 1); #Result: {x + 1, x + alpha}

p := 2:
w := 3:
pol := alpha^3 + alpha^2 + alpha - 1:
f := PolynomialToFx(x^3 + alpha*x + x^2 + alpha + x + 1, p, pol):
EDF(f, p, w, pol, 2); #Result {x + 1, x^2 + alpha + 1}
